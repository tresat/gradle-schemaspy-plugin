buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    
    jcenter()
  }
  
  dependencies {
    classpath 'com.gradle.publish:plugin-publish-plugin:0.9.0'
  }
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'eclipse'

description = 'Gradle SchemaSpy plugin'
sourceCompatibility = 1.7
group = 'com.tomtresansky.gradle.plugin.schemaspy'
version = '0.2'

repositories {
  mavenCentral()
}

dependencies {
  def groovy = [group:'org.codehaus.groovy', name:'groovy-all', version:'2.3.9']
  
  compile gradleApi()
  compile groovy
  
  compile 'com.google.guava:guava:18.0'
  compile 'org.jumpmind.symmetric.schemaspy:schemaspy:5.0.0'
}

eclipse {
  project {
    name = 'Gradle-SchemaSpy-Plugin'
    natures += 'org.springsource.ide.eclipse.gradle.core.nature'
  }

  classpath {
    // Override default setting and download and associate Javadoc
    downloadJavadoc = true
  }
}

/*
 * Remove 'default classpath' from generated build paths, else the project root becomes a source directory.
 */
eclipse.classpath.file {
  // Closure executed after .classpath content is loaded from existing file and after gradle build information is merged
  whenMerged { classpath ->
  	classpath.entries.removeAll { entry -> 
  	  return (entry.kind == 'src' && entry.path == "/${eclipse.project.name}")
  	}
  }
}

pluginBundle {
  website = 'https://github.com/tresat/gradle-schemaspy-plugin'
  vcsUrl = 'https://github.com/tresat/gradle-schemaspy-plugin'

  // The plugins block can contain multiple plugin entries.
  //
  // The name for each plugin block below (greetingsPlugin, goodbyePlugin)
  // does not affect the plugin configuration, but they need to be unique
  // for each plugin.

  // Plugin config blocks can set the id, displayName, version, description
  // and tags for each plugin.

  // id and displayName are mandatory.
  // If no version is set, the project version will be used.
  // If no tags or description are set, the tags or description from the
  // pluginBundle block will be used, but they must be set in one of the
  // two places.

  plugins {
    schemaspyPlugin {
      id = 'com.tomtresansky.gradle.plugin.schemaspy'
      displayName = 'Gradle Schema Spy Plugin'
      description = 'A plugin for Gradle to allow for running SchemaSpy as a part of the buildDashboard plugin.'
      tags = ['gradle', 'plugin', 'schemaspy', 'buildDashboard', 'reporting', 'database']
    }
  }

  // Optional overrides for Maven coordinates.
  // If you have an existing plugin deployed to Bintray and would like to keep
  // your existing group ID and artifact ID for continuity, you can specify
  // them here.
  //
  // As publishing to a custom group requires manual approval by the Gradleware
  // team for security reasons, we recommend not overriding the group ID unless
  // you have an existing group ID that you wish to keep. If not overridden,
  // plugins will be published automatically without a manual approval process.

  mavenCoordinates {
    groupId = 'com.tomtresansky.gradle.plugin.schemaspy'
    artifactId = 'gradle-schemaspy-plugin'
  }
}
